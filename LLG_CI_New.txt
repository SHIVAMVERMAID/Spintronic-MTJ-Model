** Title:  LLG_CI_New.txt
** Author: jagadish iit(bhu),varanasi
** Email:  jagadishstudent.rs.ece19@itbhu.ac.in
************************************************************************************
***** external magnetic field mT(1) covert to Oe(10)
***** C-I Implementation of LLG
***** cgs units employed
***** Ms = emu/cc
***** Hk = Oersted
***** Based on: Panagopoulos et. al. 2012, Framework for MTJ modeling
***** Isx Isy Isz = Spin-Current Inputs in various directions (in Amperes)
***** HBx HBy HBz = Magnetic Field in various directions  (in Oersteds)

.subckt llg_solver theta phi Mx My Mz Isx Isy Isz Ishex Ishey Ishez Ias hthx hthy hthz Tmp hp='hp' alpha='0.01' Hk='100' Vol='4e-17' Ms0='Ms0' H_app='400'

.param pi='3.1416'
.param gamma='1.76*1e7'
.param hbar='6.62*1e-34/2/pi'
.param q='1.6*1e-19'

.param s1(x)='sin(x)'
.param c1(x)='cos(x)'
.param c2(x)='cos(x)*cos(x)'

.param Nx='0.02'
.param Ny='0.02'
.param Nz='0.94'

.param Tcurie='1420'
.param beta='0.4'
.param asp='2e-5'
.param Kp='1.2e7'
.param Tmp0='300'

*************** Temp dependent parameters *******************
.param Tcurie='1420'
.param beta='0.4'
.param asp='2e-5'
.param H_app='400'

E_Ms Ms 0 vol='Ms0*(1-v(Tmp)/Tcurie)^beta'

***************Conversion factor for spin-current************
.param conv='hbar/2/q*1e7/Ms0/Vol/Hk'

EHSX hsx 0 vol='conv*(v(Isx)*v(Ias)+v(Ishex))'
EHSY hsy 0 vol='conv*(v(Isy)*v(Ias)+v(Ishez))'
EHSZ hsz 0 vol='conv*(v(Isz)*v(Ias)+v(Ishey))'

EHSX1 hsx1 0 vol='conv*v(Ishex)'
EHSY1 hsy1 0 vol='conv*v(Ishez)'
EHSZ1 hsz1 0 vol='conv*v(Ishey)'

EMX Mx 0 vol='c1(v(phi))*s1(v(theta))'
EMY My 0 vol='s1(v(phi))*s1(v(theta))'
EMZ Mz 0 vol='c1(v(theta))'

*** Demagnetizating field for in-plane MTJ *****************
E_Hdx Hdx 0 vol='-Nx*v(Mx)*v(Ms)'
E_Hdy Hdy 0 vol='-Ny*v(My)*v(Ms)'
E_Hdz Hdz 0 vol='-Nz*v(Mz)*v(Ms)'

*******Conversion factor for magnetic fields****************
E_Happx HBx 0 vol = 'H_app'
E_Happy HBy 0 vol = '0.0'
E_Happz HBz 0 vol = '0.0'

E_Hiz Hiz 0 vol='2*Kp/v(Ms)*v(Mz)'

EHX hx 0 vol='(v(HBx)+v(hthx)+v(Hdx))/Hk'
EHY hy 0 vol='(v(HBy)+v(hthy)+v(Hdy))/Hk'
EHZ hz 0 vol='(v(HBz)+v(hthz)+v(Hiz)+v(Hdz))/Hk'

C__THETA theta 0 c='(1+alpha*alpha)/(gamma*Hk)'

GTHETA_uniax 0 theta cur='-alpha*s1(v(theta))*c1(v(theta))'
GTHETA_demag 0 theta cur='-hp*(s1(v(phi))+alpha*c1(v(theta))*c1(v(phi)))*s1(v(theta))*c1(v(phi))'
GTHETA_ext   0 theta cur='alpha*c1(v(theta))*s1(v(phi))*v(hy)-c1(v(phi))*v(hy)+s1(v(phi))*v(hx)-alpha*s1(v(theta))*v(hz)+alpha * c1(v(theta)) * c1(v(phi))*v(hx)'
GTHETA_spin  0 theta cur='v(hsy)*c1(v(theta))*s1(v(phi))+alpha*v(hsy)*c1(v(phi))+v(hsx)*c1(v(theta))*c1(v(phi))-v(hsx)*s1(v(phi))*alpha-v(hsz)*sin(v(theta))'
GTHETA_she  0 theta cur='v(hsy1)*c1(v(theta))*s1(v(phi))+alpha*v(hsy1)*c1(v(phi))+v(hsx1)*c1(v(theta))*c1(v(phi))-v(hsx1)*s1(v(phi))*alpha-v(hsz1)*sin(v(theta))'

C__PHI phi 0 c='(1+alpha*alpha)/(gamma*Hk)'

GPHI_uniax 0 phi cur='-cos(v(theta))'
GPHI_demag 0 phi cur='hp*(-c2(v(phi))*c1(v(theta))+alpha*c1(v(phi))*s1(v(phi)))'
GPHI_ext   0 phi cur='-v(hz) +(-alpha*v(hx)*s1(v(phi)) + v(hx)*c1(v(theta) )*c1(v(phi)))/s1(v(theta))+ ( alpha*v(hy)*c1(v(phi))+ v(hy)*c1(v(theta))*s1(v(phi)))/s1(v(theta))'
GPHI_spin  0 phi cur='v(hsz)*alpha + 1/sin(v(theta))*(v(hsy)*(-alpha*c1(v(theta))*s1(v(phi))+ c1(v(phi)))+ v(hsx)*(-alpha*c1(v(phi))*c1(v(theta))-s1(v(phi))))'
GPHI_she  0 phi cur='v(hsz1)*alpha + 1/sin(v(theta))*(v(hsy1)*(-alpha*c1(v(theta))*s1(v(phi))+ c1(v(phi)))+ v(hsx1)*(-alpha*c1(v(phi))*c1(v(theta))-s1(v(phi))))'
.ends


